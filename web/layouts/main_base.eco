import '../data/nav.eco' as NavData;
import 'base.eco' as BaseLayout;

Web.inherit(BaseLayout);

Web.stack('head', html {
  tag 'link' with href: '/css/components/header-nav.css', rel: 'stylesheet', type: 'text/css';
});

Web.stack('footerJs', html {
  tag 'script' with src: '/js/components/header-nav.js' { }
});

fn drawNav(noSub) => html {
  var navId = Web.layout('navId');
  var subNavId = Web.layout('subNavId');

  fn drawDropdownMenu(item) => html {
    fn drawSubItem(subItem) => html {
      tag 'a' with
        class: Web.makeClass({
          'dropdown-item': true,
          'active': subNavId != null and subItem.navId == subNavId
        }),
        href: subItem.url
      {
        if subItem.icon != null {
          tag 'img' with class: 'a-icon', src: subItem.icon;

          write subItem.text;
        }
      }
    };

    tag 'div' with
      class: 'dropdown-menu', 
      'area-labelledby': item.navId .. '-dropdown-link'
    {
      // Draw each sub item
      foreach i, subItem in item.sub {
        write drawSubItem(subItem);
      }
    }
  };

  fn drawNavLink(item) => html {
    var drawSub = !noSub and item.sub != null; 

    // Draw a-tag
    tag 'a' with
      class: Web.makeClass({
        'nav-link': true,
        'dropdown-toggle': drawSub
      }),
      href: drawSub ? '#' : item.url,
      id: drawSub ? (item.navId .. '-dropdown-link') : null,
      'data-toggle': drawSub ? 'dropdown' : null
    {
      if item != null {
        tag 'img' with class: 'a-icon', src: item.icon;

        write item.text;
      }
    }

    // Draw dropdown menu
    if drawSub {
      write drawDropdownMenu(item);
    }
  };

  // Draw nav
  foreach i, item in NavData.nav {
    tag 'li' with
      class: Web.makeClass({
        'nav-item': true,
        'active': navId != null and item.navId == navId,
        'dropdown': !noSub and item.sub != null
      })
    {
      write drawNavLink(item);
    }
  }
};

Web.view(fn() => html {
  // Mobile nav
  tag 'nav' with 
    class: 'header-nav-mobile-pane header-nav-mobile navbar navbar-inverse bg-inverse'
  {
    // Mobile nav button
    tag 'button' with
      class: 'navbar-toggler navbar-toggler-right',
      type: 'button',
      'data-toggle': 'header-nav'
    {
      tag 'span' with class: 'navbar-toggler-icon' { }
    }

    // Title
    tag 'span' with class: 'navbar-brand' { write 'Francessco.us'; }

    // Nav links
    tag 'ul' with class: 'navbar-nav' {
      write drawNav(noSub: false);
    }
  }

  tag 'button' with class: 'header-nav-mobile-pane-cancel' { }

  // Desktop nav
  tag 'header' with class: 'header-nav-desktop-pane' {
    tag 'nav' with class: 'navbar navbar-toggleable-sm navbar-inverse bg-inverse' {
      tag 'div' with class: 'header-nav-mobile text-center' {
        tag 'button' with 
          class: 'header-toggler navbar-toggler-left', 
          type: 'button', 
          'data-toggle': 'header-nav'
        {
          tag 'span' with class: 'navbar-toggler-icon' { }
        }

        tag 'span' with class: 'navbar-brand' {
          write Web.layout('title');
        }
      }

      tag 'div' with class: 'container header-nav-desktop' {
        tag 'a' with class: 'navbar-brand', href: '/' {
          write 'Francessco.us';
        }

        tag 'ul' with class: 'navbar-nav mr-auto' {
          write drawNav(noSub: true);
        }
      }
    } // nav
  } // header

  // Sub view
  write Web.drawView();

  // Footer credit
  var footerCredit = Web.layout('footerCredit');

  // Mobile footer credit
  if footerCredit != null {
    tag 'p' with class: 'text-muted hidden-md-up mobile-footer-credit' {
      write 'Credit: ' .. footerCredit;
    }
  }

  // Footer
  tag 'footer' with class: 'footer' {
    tag 'div' with class: 'container' {
      tag 'span' with class: 'text-muted' { write '&copy; 2018 Ethan Lafrenais'; }

      // Desktop footer credit
      if footerCredit != null {
        tag 'span' with class: 'text-muted hidden-sm-down float-right' {
          write 'Credit: ' .. footerCredit;
        }
      }
    } // .container
  } // .footer
});